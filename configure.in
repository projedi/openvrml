AC_INIT(libvrml97core/src/vrml97/VrmlScene.cpp)
PACKAGE=libvrml97
LIBVRML97_MAJOR_VERSION="0"
LIBVRML97_MINOR_VERSION="8"
LIBVRML97_MICRO_VERSION="2"
VERSION="${LIBVRML97_MAJOR_VERSION}.${LIBVRML97_MINOR_VERSION}.${LIBVRML97_MICRO_VERSION}pre"
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)

AC_DEFINE_UNQUOTED(LIBVRML97_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(LIBVRML97_MINOR_VERSION)
AC_DEFINE_UNQUOTED(LIBVRML97_MICRO_VERSION)

dnl
dnl libtool versions
dnl
LIBVRML97_SO_VERSION="1:0:0"
LIBVRML97GL_SO_VERSION="0:1:0"
LIBVRML97JS_SO_VERSION="0:0:0"

AC_SUBST(LIBVRML97_SO_VERSION)
AC_SUBST(LIBVRML97GL_SO_VERSION)
AC_SUBST(LIBVRML97JS_SO_VERSION)

dnl
dnl Checks for Win32 stuff
dnl
AC_CYGWIN
AC_MINGW32

dnl
dnl Checks for programs.
dnl
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL
AC_PROG_INSTALL
AM_PROG_LEX
AC_PROG_YACC
AC_PROG_CXX
AC_PROG_CXXCPP

dnl
dnl Check for libjpeg
dnl
VRML_PATH_JPEG(, [libjpeg not found. Check 'config.log' for details.])

dnl
dnl Check for libpng, zlib
dnl
VRML_PATH_PNG(, [libpng or zlib not found. Check 'config.log' for details.])

dnl
dnl Check for X, GL/Mesa, glut
dnl
VRMLGL_PATH_GLUT(, AC_MSG_ERROR([glut or one of its dependencies not found. See 'config.log' for details.]))

dnl
dnl Test for adequate namespace support in the C++ compiler
dnl
VRML_CXX_NAMESPACE(
  ,
  no_cxx_namespaces=true
)
AM_CONDITIONAL(NO_CXX_NAMESPACES, test x$no_cxx_namespaces = xtrue)

VRML_CXX_BOOL(
  ,
  AC_MSG_ERROR([Your C++ compiler does not have the required support for the 'bool' keyword. See 'config.log' for details.])
)


dnl Checks for library functions.
AC_CHECK_FUNCS(ftime getcwd gettimeofday mktime strdup strerror strstr)

dnl
dnl Modulate debug mode
dnl
AC_ARG_ENABLE(debug,
  [  --enable-debug          Turn on debugging],
  [
    case "${enableval}" in
      yes) debug=true ;;
      no)  debug=false ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
    esac
  ],
  [ debug=false ]
)
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

dnl
dnl Stuff for JavaScript
dnl
AC_DEFINE(XP_UNIX, ,[JavaScript requires this])

dnl
dnl JavaScript doesn't include config.h, so we have to use automake conditionals
dnl
case "$target" in
  *-freebsd*)
    freebsd=true
    ;;
esac
AM_CONDITIONAL(FREEBSD, test x$freebsd = xtrue)

AC_OUTPUT(Makefile \
          m4/Makefile \
          libvrml97js/Makefile \
          libvrml97js/src/Makefile \
          libvrml97js/src/vrml97js/Makefile \
          libvrml97js/src/vrml97js/fdlibm/Makefile \
          libvrml97core/Makefile \
          libvrml97core/m4/Makefile \
          libvrml97core/src/Makefile \
          libvrml97core/src/vrml97/Makefile \
          libvrml97gl/Makefile \
          libvrml97gl/m4/Makefile \
          libvrml97gl/src/Makefile \
          libvrml97gl/src/vrml97gl/Makefile \
          lookat/Makefile \
          lookat/src/Makefile)
