AC_INIT(src/openvrml/OpenVRML/VrmlScene.cpp)

PACKAGE=openvrml
OPENVRML_MAJOR_VERSION="0"
OPENVRML_MINOR_VERSION="12"
OPENVRML_MICRO_VERSION="0"
VERSION="${OPENVRML_MAJOR_VERSION}.${OPENVRML_MINOR_VERSION}.${OPENVRML_MICRO_VERSION}"
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)

AC_DEFINE_UNQUOTED(OPENVRML_MAJOR_VERSION)
AC_DEFINE_UNQUOTED(OPENVRML_MINOR_VERSION)
AC_DEFINE_UNQUOTED(OPENVRML_MICRO_VERSION)

dnl
dnl Variables to build libantlr as a convenience library
dnl
NOINSTALL_LIBANTLR=libantlr.la
AC_SUBST(INSTALL_LIBANTLR)
AC_SUBST(NOINSTALL_LIBANTLR)
AC_SUBST(LIBANTLR_RPATH_FLAG)
AC_SUBST(ANTLR_INCLUDEDIR)
AM_CONDITIONAL(ANTLR_INSTALL_HFILES, test "apple" = "orange")

dnl
dnl Checks for Win32 stuff
dnl
AC_CYGWIN
AC_MINGW32

dnl
dnl Checks for programs.
dnl
AC_ENABLE_STATIC(yes)
enable_static=yes
AM_PROG_LIBTOOL
AC_PROG_CXX
AC_PROG_CXXCPP


dnl Checks for library functions.
AC_CHECK_FUNCS(ftime getcwd gettimeofday mktime strdup strerror strstr)

OV_CXX_BOOL(, AC_MSG_ERROR([Your C++ compiler does not have the required support for the 'bool' keyword.  See 'config.log' for details.]))
OV_CXX_NAMESPACE(
  ,
  no_cxx_namespaces=true
)
AM_CONDITIONAL(NO_CXX_NAMESPACES, test x$no_cxx_namespaces = xtrue)

OV_CXX_AUTO_PTR(, AC_MSG_ERROR([auto_ptr not found]))
OV_CXX_HASH_MAP

OV_PATH_ZLIB(, no_zlib=yes)
if test "X$no_zlib" = "Xyes"; then
  AC_MSG_ERROR([zlib not found.])
fi

OV_PATH_LIBJPEG(, AC_MSG_ERROR([libjpeg not found.  Use '--with-libjpeg=PREFIX' to locate the library or '--without-libjpeg' to build without JPEG support.]))
OV_PATH_LIBPNG(, AC_MSG_ERROR([libpng not found.  Use '--with-libpng=PREFIX' to locate the library or '--without-libpng' to build without PNG support.]))
OV_PATH_MOZJS(
  AC_DEFINE(OPENVRML_HAVE_JS, , [defined if libmozjs is available and should be used]),
  AC_MSG_ERROR([SpiderMonkey (libmozjs) not found.  Use '--with-mozjs=PREFIX' to locate the library or '--without-mozjs' to build without JavaScript support.])
)
OV_PATH_JAVA(, AC_MSG_ERROR([JDK not found. Use '--with-java=PREFIX' to locate the JDK or '--without-java' to build without java support.]))

dnl
dnl Check for X, GL/Mesa, glut
dnl
OV_PATH_GLUT(, AC_MSG_ERROR([glut or one of its dependencies not found.  See 'config.log' for details.]))


dnl
dnl Modulate debug mode
dnl
AC_ARG_ENABLE(debug,
  [  --disable-debug         Turn off debugging],
  [
    case "${enableval}" in
      yes) debug=true ;;
      no)  debug=false ;;
      *)   AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
    esac
  ],
  [ debug=true ]
)
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

AC_OUTPUT(Makefile \
          doc/Makefile \
          macros/Makefile \
          models/Makefile \
          models/audio/Makefile \
          models/textures/Makefile \
          lib/Makefile \
          lib/antlr/Makefile \
          src/Makefile \
          src/openvrml/Makefile \
          src/openvrml/OpenVRML/Makefile \
          src/openvrml-gl/Makefile \
          src/openvrml-gl/OpenVRML/Makefile \
          src/openvrml-gl/OpenVRML/GL/Makefile \
          src/lookat/Makefile \
          java/Makefile \
          java/vrml/Makefile \
          java/vrml/node/Makefile \
          java/vrml/field/Makefile \
          Doxyfile \
          openvrml.spec)
